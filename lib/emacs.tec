!* emacs.tec--do emacs-style motion and editing in TECO !
!*  Clearly not a complete emulation, but sometimes convenient !
!*  for moving about. !
@^ue\

!* Preserve Q registers !
[k

!* Clear kill buffer !
^uk

!* No-echo, no-wait TTY, small window !
1,7:w
et#40et

!* CR-LF flag!
0ur

!* Dot !
.ud

!* endless loop for commands !
<

 !* advance undo versioning !
 :fu

 !* Read key into Qa, update and sleep if none available !
 ^tua qa+1"e 1w et-32et ^tua et#40et'

 !* Bump command counter !
 %e

 !* If text, insert and iterate !
 qa-31"g qai f<'

 !* Map delete key to erase !
 qa-127"e -1d f<'

 !* Else vector out to actions for supported keys !
 qa@o/N,CA,CB,N,CD,CE,CF,N,CH,CI,CJ,CK,CL,CM,CN,CO,CP,CQ,N,N,N,N,CV,CW,CX,CY,N,ES/

 !* Ignore !
!N!
 f<

 !* Start of line !
!CA!
 0l f<

!CB!
 1r f<

!CD!
 1d f<

!CE!
 1l z-."g 2r ' f<

!CF!
 1c f<

!CH!
 -1d f<

!CI!
 @i/	/ f<

!CJ!
 qr"e
  .ub
  0l .ua 
  <::@s/^Es/;>
  qa,.xa
  qbj @i/
/ ga
 '
 0ur
 f<

!CK!
 !* If we've moved since last ^k, clear kill buffer !
 qe-1"n ^uk'

 !* Reset kill buffer motion counter !
 0ue

 !* Delete empty line, or delete to end of line !
 .ua 1l2r
 qa-."e 1:xk 1k | qa,.:xk qa,.k'
 f<

!CL!
 0,7:w 1,7:w f<

!CM!
 1ur
 @i/
/ f<

!CN!
 .ua 0l qa-.ub
 1l qb<::@s/^El/"s 1r 1; | 1c'> f<

!CO!
 .ua @i/
/ qaj f<

!CP!
 .ua 0l .uc  qa-.ub 
 -1l qbc 
 .-qc+1"g qcj  2r'
 f<

!CQ!
 @i/^Q/ 
 ^tua qa+1"e 1w et-32et ^tua et#40et' 
 -2d  qai
 f<

!CV!
 24l f<

!CW!
 qd-."g .,qd:xk .,qdk | qd,.:xk qd,.k'
 .ud f<

!CX!
 !* ^X-char; get next char to complete command !
 ^tua qa+1"e 1w et-32et ^tua et#40et'

 !* ^X-u, undo !
 qa-^^u"e -1fu f<'

 !* ^X-r, redo !
 qa-^^r"e 1fu f<'

 !* ^X-^X, swap point and mark !
 qa-24"e .ua qdj qaud f<'

 !* All others--ignore !
 f<

!CY!
 !* Insert kill buffer !
 gk f<

!ES!
 !* Get arg char for ESC !
 ^tua qa+1"e 1w et-32et ^tua et#40et'

 !* ESC-v goes back a page !
 qa-^^v"e -24l f<'

 !* ESC-< goes to top of file !
 qa-^^<"e .ud 0j f<'

 !* ESC-> goes to bottom of file !
 qa-^^>"e .ud zj f<'

 !* ESC-p positions screen; gets one more char !
 qa-^^p"e .,6:w f<'

 !* ESC-space, set mark !
 qa-^^ "e .ud f<'

 !* ESC-ESC--go back to TECO !
 qa-^^"e
  5,7:w  et-40et
  ]k
  ^c
 '

 !* All others--no-op !
 f<
>

\
