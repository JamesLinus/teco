!* book.tec--book viewer !
!*  Remembers last viewed location, and returns to it !
!*  Command chars are read into qa numeric !
!*  File position is in qb numeric !
!*  File name being viewed is in qb text !
!*  Previous location kept in qc numeric !
!*  Constructed commands are assembled in qc text !
@o/nobook/ 

!book!

@^uv~
!* Get filename to view into qb !
hk gz 0j  @:s/ /  .,zxb 
!* Build ':er<file>.next' into qc, execute it to get line # !
hk0j@i/:er/ gb  @i/.next/  hxc hk  mc"e 0ub | a 0j  \ub hk' 
!* Build ':er<file>.txt' into qc, execute it to get file contents !
hk0j@i/er/ gb  @i/.txt/  hxc hk  mc  a 

!* Jump to recorded position !
qbj 

!* No-echo, no-wait TTY, small window !
5,7:w
et#40et

!* endless loop for commands !
<

 !* Read key into Qa, update and sleep if none available !
 ^tua qa+1"e .,6:w  1w  et-32et ^tua et#40et'

 !* Space bar--advance one page !
 qa-^^ "e 2:w-1l  f<'

 !* b--back one page !
 qa-^^b"e -1*(2:w-1)l  f<'

 !* d--forward half page !
 qa-^^d"e 10l  f<'

 !* u--back half page !
 qa-^^u"e -10l  f<'

 !* g--go to top !
 qa-^^g"e .uc 0j f<'

 !* G--go to bottom !
 qa-^^G"e .uc zj  -1l f<'

 !* '--jump to previous !
 qa-^^'"e .ua qcj qauc f<'

 !* return--forward one line !
 qa-13"e l f<'

 !* j--forward one line !
 qa-^^j"e l f<'

 !* k--backward one line !
 qa-^^k"e -l f<'

 !* ^G--display position !
 qa-7"e
  :gb @^A/.txt line / 
  !* Count lines to current position to Qd !
  .ua 0ud bj  <.-qa"l l 1%d f>' 0;> qd:= 
  @^A/ char / qaj .:= 
  !* Show percentage of file !
  @^A/ (/ .*100/z:= @^A/%)/ 
  f<
 '

 !* q--done !
 qa-^^q"e 0; '

 !* All others--no-op !
 f<
> 

!* Save position back to <file>.next !
.ub hkek  @i/ew/ gb @i/.next/  hxc  mc 
hk qb\ 

!* All done !
ex 
~ mv 

!nobook!


