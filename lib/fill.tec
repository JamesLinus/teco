!* mf--Fill paragraph point lies within, written all in TECO !
!* Optional arg is line length, default is 70 !
!* Textual Q regs: !
!*  t - current word being filled into output !
!*  u - list of space chars !
!*  v - prefix text on each line !
!* Number Q regs: !
!*  a - current position in input !
!*  b - end position of input !
!*  c - current output column !
!*  d - flag first word in line !
!*  p - current output position !
!*  s - position of start of input !
@^uf\
+0ua
qa"e 70um | qaum '

!* Save text registers we scribble on !
[t[u[v[w

!* Leading quotation? !
0l ::@s/> /"s 0l
 !* Record prefix !
 @^uv/> /

 !* Identify paragraph to fill by presence of quotations !
 < ::@s/> /"u 1l 1; ' 0l ."e 1; ' -l > .ua
 < .-z"e 1; ' ::@s/> /; 1l > .ub
|
 !* No prefix !
 @^uv//

 !* Find paragraph by block of text, all starting with alnum !

 !* Find start of this paragraph !
 <
  ::@s/R/"u
   !* Failing line included if indented (leading spaces) !
   ::@s/S/"u 1l ' 1;
  '
  0l ."e 1; ' -1l
 > .ua

 !* Walk forward to line without alnum header !
 < l ::@s/R/; > .ub
'

!* Avoid zero-length operations !
qa-qb"e Empty paragraph specified
 ^c '

!* text to fill is in qa..qb !
!* qv has any prefix/quotation string !

!* Start of range in qs, so we can delete original text after fill !
qaus

!* Build the filled text right below the unfilled text, jumping back !
!* and forth !

!* QPn is our current output position !
qbup
!* QCn is our current output column !
0uc
!* Start of line--first word being added !
1ud
!* Set QU to white space chars , QW to white plus newline !
@^uu/ 	/
@^uw/ 	
/

!* Honor leading quote !
:qv%a  qpj gv .up :qvuc

!* Honor leading white space !
qaj
<
 ::@s/gu/;
 1r
 ::@s/	/"s 8%c | 1%c 1c '
>
!* Copy leading white space via qt !
qa,.xt .ua
qpj gt .up

!* While words, assemble and fill !
<
 !* Skip leading white space !
 qaj
 <::@s/gu/;>
 !* At new line, skip any quote !
 ::@s/^EL/"s 1l .-qb"l  :qvc ' '

 !* Walk across word, pull into QT !
 .ua
 <::@s/gw/"s 1r 1; | 1c '>
 qa,.xt
 .ua

 !* If we're past end of range we want, terminate loop !
 qa-qb;

 !* If it won't fit on the line, terminate current line and start over !
 qc+:qt+1-qm"g
  qpj @i/
/ gv :qvuc .up 1ud
 '

 !* Put space(s) between words !
 qpj qd"e
  1r @::s/./"s @i/ / 1%c | 1c ' !* Two spaces between sentences !
  @i/ / 1%c !* Space between words !
 | 0ud
 '

 !* Add in our word, update column counter !
 gt .up
 :qt%c 
>

!* Chop off unfilled text, add trailing newline !
qpj
@i/
/
qs,qbk
qsj

!* Restore text registers we scribble on !
]u]t]v]w
\

